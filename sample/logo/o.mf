% This is a comment. Comments begin with a '%' and extends to the end of line.

% Global variables defining the sizes:
numeric size, hsize;
% Now we begin defining 'O'. Each glyph has a corresponding string description,
% a horizontal size, vertical size and depth. (when you want do draw below
% the baseline)
beginchar("O", hsize, size, 0);
  % Pair variables can be used to describe points in 2D space:
  pair z1, z2, z3, z4, z5, z6, z7;
  % Numeric variables are floating-point values:
  numeric pen_width, pen_height;
  % Initializing one of the variables. In a glyph definition, we
  % can use an implicit variable 'h', which is our glyph height:
  pen_height = 0.09*h;
  % Initializing other variable:
  pen_width = 0.9*pen_height;
  % Before drawing, we need to pick up a pen. Here we use a pen called
  % 'pencircle', which is a circle of 1 pixel. As this is too small, we
  % scale it in coordinates 'x' and 'y' to the desired size:
  pickup pencircle yscaled pen_height xscaled pen_width;
  % Our point 'z1' is defined as the position of our pen, if we place
  % it immediately to the right (rt) of point (0, 0.25*h).
  % Where 'h' is our glyph height.
  z1 = rt (0, 0.25*h);
  % Our point 'z2' is defined as the position of our pen, if we place
  % it immediately to the right (rt) of point (0, 0.75*h).
  % Where 'h' is our glyph height.
  z2 = rt (0, 0.75*h);
  % Our point 'z3' is defined as the position of our pen, if we place
  % it immediately on the bottom (bot) of point (0.5*w, h).
  % Where 'w' is our glyph width and 'h' is our glyph height.
  z3 = bot (0.5*w, h);
  % Our point 'z4' is defined as the position of our pen, if we place
  % it immediately to the left (lft) of point (w, 0.75*h).
  % Where 'w' is our glyph width and 'h' is our glyph height.
  z4 = lft (w, 0.75*h);
  % Our point 'z5' is defined as the position of our pen, if we place
  % it immediately to the left (lft) of point (w, 0.25*h).
  % Where 'w' is our glyph width and 'h' is our glyph height.
  z5 = lft (w, 0.25*h);
  % Our point 'z5' is defined as the position of our pen, if we place
  % it immediately on the top of point (0.5*w, 0).
  % Where 'w' is our glyph width.
  z6 = top (0.5*w, 0);
  % After describing our pen and all the points, we draw our 'O'. Connecting
  % two points with '--' creates a line and connecting with '..' creates
  % a curve. The numbers between brackets '{}' are unit vectors
  % representing directions: it is the pen direction when it walks through
  % the path and is in a given point. They are hints about how do we
  % want the curve. The last point can be replaced by keyword 'cycle'
  % if it is equal the first one. The information that we are drawing a
  % cyclic path usually helps the algorithm to create more pleasant curves.
  draw z1--z2{0, 1}..z3{1, 0}..{0, -1}z4--z5{0,-1}..{-1, 0}z6..{0, 1}cycle;
endchar;

numeric size, weight, italic;

size = 10;
weight = 1;
italic = 0;

beginchar("!", (0.13+0.067*(weight-1)+0.12*italic)*size*pt, 0.667*size*pt, 0.012 * size * pt);
  numeric circle_diameter;
  % Drawing dot
  circle_diameter = ((0.192 + 0.1 * (weight-1)) * h);
  pickup pencircle scaled circle_diameter;
  draw (top rt (0, -d));
  % Drawing body
   pen body;
   pair p1, p2, p3, p4, p5, p6;
   p1 = (circle_diameter/2-(0.033+0.045*(weight-1))*h, circle_diameter-d+(0.125-0.03*(weight-1))*h);
   p2 = (circle_diameter/2+(0.033+0.045*(weight-1))*h, circle_diameter-d+(0.125-0.03*(weight-1))*h);
   p3 = (circle_diameter/2-(0.06+0.08*(weight-1))*h, circle_diameter-d+(0.55-0.1*(weight-1))*h);
   p4 = (circle_diameter/2+(0.06+0.08*(weight-1))*h, circle_diameter-d+(0.55-0.1*(weight-1))*h);
   p5 = (circle_diameter/2-(0.06+0.08*(weight-1))*h, h);
   p6 = (circle_diameter/2+(0.06+0.08*(weight-1))*h, h);
   p1 = p1 + (italic * 0.2 * (w-xpart p6), 0);
   p2 = p2 + (italic * 0.2 * (w-xpart p6), 0);
   p3 = p3 + (italic * 0.65 * (w-xpart p6), 0);
   p4 = p4 + (italic * 0.65 * (w-xpart p6), 0);
   p5 = p5 + (italic * (w-xpart p6), 0);
   p6 = p6 + (italic * (w-xpart p6), 0);
   body = makepen (p1--p2--p4--p6--p5--p3--cycle);
   pickup body;
   draw (0, 0);
endchar;

beginchar("\"", (0.225+0.108*(weight-1))*size*pt, 0.667*size*pt, -(0.445-0.0125*(weight-1))*size*pt);
  pen body;
  pair p1, p2, p3, p4, t;
  numeric s;
  if w < 9:
    s = -0.034;
  else:
    s = 0;
  fi
  p3 = (0*w, h);
  p4 = ((0.35+s+0.075*(weight-1))*w, h);
  p1 = (((xpart p4)*0.5 -(0.1+0.075*(weight-1))*w), -d);
  p2 = (((xpart p4)*0.5 +(0.1+0.075*(weight-1))*w), -d);
  body = makepen(p1--p2--p4--p3--cycle);
  pickup body;
  draw (0, 0);
  t = (w-xpart p4, 0);
  draw t;
endchar;

beginchar("#", (0.579+0.158*(weight-1))*size*pt, 0.668*size*pt, 0);
  pickup pensquare scaled ((0.12+0.090*(weight-1)) * h) slanted 0.15;
  draw rt (0, (0.31-0.02*(weight-1)) * h) --
    lft (0.948 * w, (0.31-0.02*(weight-1)) * h);
  draw rt (0.0517 * w, (0.68+0.02*(weight-1)) * h) --
    lft (w, (0.68+0.02*(weight-1)) * h);
  draw top ((0.241-0.01*(weight-1)) * w, 0) -- bot ((0.396-0.01*(weight-1)) * w, h);
  draw top ((0.595+0.01*(weight-1)) * w, 0) -- bot ((0.75+0.01*(weight-1)) * w, h);
endchar;

beginchar("$", (0.517+0.057*(weight-1)+0.01*italic+0.015*italic*(weight-1))*size*pt,
    0.745*size*pt, 0.0875*size*pt);
  renderchar "S" between (0, 0) and (w, h-d);
  pair p7, p8;
  pickup pensquare scaled ((0.08+0.025*(weight-1)-0.02*italic) * (h+d));
  p7 = bot ((0.5-0.152*italic)*w, -d);
  p8 = top ((0.5+0.152*italic)*w, h);
  draw p7--p8;
endchar;


beginchar("%", (0.852+0.08*(weight-1)+0.025*italic)*size*pt, 0.68*size*pt, 0);
  path circle;
  pair p1, p2, p3, p4;
  pickup pencircle scaled (0.108+0.065*(weight-1)) * h;
  p1 = rt (0, 0.103*h);
  p2 = bot (0.103*h, h);
  p3 = top (w-0.103*h, 0);
  p4 = lft (w, 0.103*h);
  circle =
    (0.5, 0)
    .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
    (0.35356, 0.35356)
    .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
    (0, 0.5);
  circle = circle & circle rotated 90;
  circle = circle & circle rotated 180 & cycle;
  circle = circle slanted (0.12 * italic);
  circle = circle scaled ((0.336-0.045*(weight-1)-0.02*italic+0.02*italic*(weight-1))*w);
  circle = circle shifted (xpart p1 - xpart (point 4 of circle),
      ypart p2 - ypart (point 2 of circle));
  draw circle;
  circle = circle shifted (xpart p4 - xpart (point 0 of circle),
      ypart p3 - ypart (point 6 of circle));
  draw circle;
  pickup pensquare xscaled (0.127+0.055*(weight-1))*h;
  draw (0.228*w, 0)--(0.772*w,h);
endchar;

beginchar("&", (0.657+0.02*(weight-1)-0.045*italic)*size*pt,
    (0.68+0.027*(weight-1))*size*pt, 0);
  pair p0, p2, p3, p4, p5, p6, p7, p8;
  path p;
  pickup pensquare xscaled (0.17+0.07*(weight-1)-0.04*italic*(weight-1))*h;
  p0 = lft (w-(0.1w*italic*(weight-1)), 0);
  p2 = ((0.356+0.082*italic+0.00*italic*(weight-1))*w, 0.561*h);
  draw p0--p2;
  pickup pencircle scaled (0.13+0.05*(weight-1)-0.01*italic*(weight-1))*h;
  p4 = bot ((0.423+0.174*italic-0.1*italic*(weight-1))*w, h);
  p3 = ((0.21+0.135*italic-0.02*italic*(weight-1))*w, ypart p2 + 0.66 * (ypart p4 - ypart p2));
  p5 = ((0.624+0.168*italic-0.09*italic*(weight-1))*w, ypart p2 + 0.61 * (ypart p4 - ypart p2));
  p6 = rt (0, 0.28*h);
  p7 = top ((0.342+0.01*italic*(weight-1)) * w, 0);
  p8 = ((0.8+0.119*italic)*w, (0.539-0.03*italic)*h);
  p8 = italic[p8, lft (w, ypart p8)];
  p = p2..p3..p4..p5..p2..p6..p7..p8;
  draw p;
  pickup pensemicircle scaled (0.13+0.05*(weight-1)-0.01*italic*(weight-1))*1.1*h;
  pickcolor(1, 1, 1, 0);
  draw p8;
endchar;

beginchar("'", (0.0825+0.0625*(weight-1))*size*pt, 0.667*size*pt, -(0.445-0.0125*(weight-1))*size*pt);
  pen body;
  pair p1, p2, p3, p4;
  p3 = (0, h);
  p4 = (w, h);
  p1 = (w/2 -(0.3+0.075*(weight-1))*w, -d);
  p2 = (w/2 +(0.3+0.075*(weight-1))*w, -d);
  body = makepen(p1--p2--p4--p3--cycle);
  pickup body;
  draw (0, 0);
endchar;

beginchar("(", (0.217+0.08*(weight-1)+0.09*italic)*size*pt,
    0.667*size*pt, 0.162*size*pt);
  pair p1, p2, p3;
  path body;
  pickup pensquare xscaled (0.11+0.11*(weight-1))*h;
  p1 = lft (w, -d);
  p1 = italic[p1, (0.35w, -d)];
  p2 = rt (0, (0.527-0.164*italic)*(h+d)-d);
  p3 = lft (0.86w, h);
  p3 = italic[p3, lft (w, h)];
  body =
    p1 .. controls
    (xpart p1 + (0.572125 * (xpart p2 - xpart p1)),
      ypart p1 + (0.275821 * (ypart p2 - ypart p1)))
    and
    (xpart p1 + (0.863152 * (xpart p2 - xpart p1)),
      ypart p1 + (0.610297 * (ypart p2 - ypart p1)))
    .. p2 .. controls
    (xpart p2 + (0.017102 * (xpart p3 - xpart p2)),
      ypart p2 + (0.476997 * (ypart p3 - ypart p2)))
    and
    (xpart p2 + (0.281357 * (xpart p3 - xpart p2)),
      ypart p2 + (0.665754 * (ypart p3 - ypart p2)))
    .. p3;
  draw body;
endchar;

beginchar(")", (0.217+0.08*(weight-1)+0.1*italic)*size*pt,
    0.667*size*pt, 0.162*size*pt);
  pair p1, p2, p3;
  path body;
  pickup pensquare xscaled (0.11+0.11*(weight-1))*h;
  p1 = rt (0, -d);
  p2 = lft (w, (0.533+0.112*italic)*(h+d)-d);
  p3 = rt (0.14w, h);
  p3 = italic[p3, rt (0.544w, h)];
  body =
    p1 .. controls
    (xpart p1 + (0.572125 * (xpart p2 - xpart p1)),
      ypart p1 + (0.275821 * (ypart p2 - ypart p1)))
    and
    (xpart p1 + (0.863152 * (xpart p2 - xpart p1)),
      ypart p1 + (0.610297 * (ypart p2 - ypart p1)))
    .. p2 .. controls
    (xpart p2 + (0.017102 * (xpart p3 - xpart p2)),
      ypart p2 + (0.476997 * (ypart p3 - ypart p2)))
    and
    (xpart p2 + (0.281357 * (xpart p3 - xpart p2)),
      ypart p2 + (0.665754 * (ypart p3 - ypart p2)))
    .. p3;
  draw body;
endchar;

beginchar("*", (0.297+0.04*(weight-1)-0.01*italic)*size*pt,
    0.667*size*pt, (-0.387+0.037*(weight-1)+0.015*italic)*size*pt);
  numeric radius;
  if w <= (h+d):
    radius = w/2;
  else:
    radius = (h+d)/2;
  fi
  pair p0, p1, p2, p3, p4, p5;
  p0 = (w/2, (h-d)/2);
  p1 = (radius, 0) rotated (16-5*italic) shifted p0;
  p2 = (radius, 0) rotated (90-5*italic) shifted p0;
  p3 = (radius, 0) rotated (164-5*italic) shifted p0;
  p4 = (radius, 0) rotated (235-5*italic) shifted p0;
  p5 = (radius, 0) rotated (305-5*italic) shifted p0;
  pen brush;
  brush = pensquare yscaled ((0.15+0.1*(weight-1))*(h-w));
  pickup brush rotated (angle (p1-p0));
  draw p0-- p1;
  pickup brush rotated (angle (p2-p0));
  draw p0-- p2;
  pickup brush rotated (angle (p3-p0));
  draw p0--p3;
  pickup brush rotated (-angle (p4-p0));
  draw p0-- p4;
  pickup brush rotated (-angle (p5-p0));
  draw p0-- p5;
endchar;

beginchar("+", (0.497+0.015*(weight-1)+0.0025*italic)*size*pt, 0.495*size*pt, 0);
  pickup pensquare scaled (0.16+0.1*(weight-1))*h;
  draw top (w/2, 0) -- bot (w/2, h);
  draw rt (0, h/2) -- lft (w, h/2);
endchar;

beginchar(",", (0.13+0.068*(weight-1)+0.03*italic-0.009*italic*(weight-1))*size*pt,
    (0.12+0.0625*(weight-1)+0.0025*italic+0.005*italic*(weight-1))*size*pt,
      (0.132+0.015*(weight-1))*size*pt);
  numeric c;
  if w < h:
    c = w;
  else:
    c = h;
  fi
  pickup pencircle scaled c;
  draw top lft (w, 0);
  pickup pensquare xscaled c/2;
  draw lft (w+0.1*c, c/2) -- rt (0, -d);
endchar;

beginchar("-", (0.292+0.035*(weight-1)+0.02*italic-0.01*italic*(weight-1))*size*pt,
    (0.29+0.0225*(weight-1))*size*pt,
    (-0.208+0.025*(weight-1))*size*pt);
  pickup pensquare scaled (h+d) slanted (0.3 * italic);
  draw rt (0, (h-d)/2) -- lft (w, (h-d)/2);
endchar;

beginchar(".", (0.13+0.068*(weight-1)+0.002*italic-0.002*italic*(weight-1))*size*pt,
    (0.118+0.065*(weight-1))*size*pt,
    0.0125*size*pt);
  pickup pencircle scaled (h+d);
  draw (w/2, (h-d)/2);
endchar;

beginchar("/", (0.35+0.065*(weight-1)+0.14*italic)*size*pt, 0.668*size*pt, 0);
  pickup pensquare xscaled (0.14+0.09*(weight-1))*h;
  draw rt (0, 0) -- lft (w, h);
endchar;

beginchar("0", (0.532+0.045*(weight-1)+0.055*italic-0.015*italic*(weight-1))*size*pt,
    0.68*size*pt, 0.0125*size*pt);
  pen dot;
  pair tmp;
  numeric wf, ang, zero_height, zero_width;
  path circle, dash;
  % Creating pen
  wf = (weight - 1);
  dot = pencircle scaled (0.11594202898550725 * h);
  dot = dot xscaled (1.3 + (0.6 * wf));
  dot = dot yscaled (1 + (0.25 * wf));
  dot = dot yscaled (1 + (0.125 * italic) + (0.125 * wf));
  pickup dot;
  % Getting path dimensions and form for the circle
  circle =
  (0.5, 0)
  .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
  (0.35356, 0.35356)
  .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
  (0, 0.5);
  circle = circle & circle rotated 90;
  circle = circle & circle rotated 180 & cycle;
  ang = -15 * italic;
  zero_height = 0.988 * (h / (cosd ang));
  zero_width = w * (cosd ang) - italic * 0.07 * size;
  tmp = (w, h) xscaled (zero_width / w);
  tmp = tmp yscaled (zero_height / h);
  tmp = tmp - (top rt (0, 0)) + (bot lft (0, 0));
  circle = circle xscaled (xpart tmp) yscaled (ypart tmp);
  circle = circle rotated ang shifted (w/2, h/2 - 2);
  draw circle;
  % Creating the dash
  dash = (-0.5, 0) -- (0.5, 0);
  dash = dash rotated -45;
  dash = dash scaled (1 - (italic * 0.1));
  dash = dash xscaled ((xpart tmp)) yscaled (ypart tmp);
  dash = dash rotated ang shifted (w/2, h/2);
  pickup dot scaled (1 - 0.3 * (weight-1) - 0.1 * italic);
  draw dash;
endchar

beginchar("1", (0.265+0.04*(weight-1)+0.035*italic+0.02*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  pair p1, p2, p3;
  pickup pensquare xscaled (0.13+0.1*(weight-1))*h;
  p1 = lft (w - 0.5w * italic, 0);
  p3 = lft (w, h);
  p2 = (0.8-0.03*(weight-1))[p1, p3];
  draw p1 -- p2;
  pickup pensquare yscaled (0.12+0.075*(weight-1)-0.02*italic)*h rotated (-10 * italic);
  draw rt (0, (0.8-0.03*(weight-1))*h) -- p2;
  p2 = p2 + ((0.065+0.05*(weight-1))*h, 0);
  pickup pensquare xscaled (0.1+0.07*(weight-1))*h ;
  draw lft p2 -- lft (w, h);
  pickup pensquare xscaled 0.1h yscaled 0.03h;
  p1 = rt bot (w-(0.1+0.07*(weight-1))*h, h);
  p2 = rt bot (0 + italic * 0.02h + italic * (weight-1) * 0.01h, (0.86+0.0075*(weight-1))*h);
  path p;
  p =
    p1 .. controls
    (xpart p1 + (0.110926 * (xpart p2 - xpart p1)),
      ypart p1 + (1.088190 * (ypart p2 - ypart p1)))
    and
    (xpart p1 + (0.679659 * (xpart p2 - xpart p1)),
      ypart p1 + (0.951881 * (ypart p2 - ypart p1)))
    .. p2;
  draw p;
  pen eraser;
  eraser = makepen((w, h)--(w, 0)--(0.065w +w - 0.5w * italic, 0)--cycle);
  pickup eraser;
  pickcolor(1, 1, 1, 0);
  erase (0,0);
endchar;

beginchar("2", (0.482+0.032*(weight-1)+0.112*italic-0.005*italic*(weight-1))*size*pt,
    0.68*size*pt, 0);
  pair begin, upper, back, basis;
  numeric ang, b;
  ang = 90 - 12 * italic;
  b = ((0.113 + 0.071 * (weight-1)) * h);
  if ang < 77:
    ang = 77;
  fi
  pickup pencircle scaled ((0.131 + 0.093 * (weight-1)) * h);
  begin = ((0.156 + 0.1 * italic) * w, 0.75 * h);
  upper = bot ((0.522) * w, h);
  back = lft ((0.963-0.02 * italic) * w, 0.774 * h);
  basis = (b*sind(12*italic), (0.03 + 0.02 * (weight-1)) * h);
  draw begin .. {1, 0}upper .. back .. tension 1.5 .. {-0.7986, -0.60181}basis;
  pickup pensquare xscaled ((0.113 + 0.071 * (weight-1)) * h) rotated 90;
  erase top (0, 0) .. top (w, 0);
  pickup pensquare xscaled ((1/sind ang)*b) rotated ang;
  draw top rt (0, 0) .. top lft (w * (1 - 0.16 * italic), 0);
  % Erasing:
  pickup pensemicircle scaled ((0.16+ 0.093 * (weight - 1))  * h) rotated 160;
  erase begin;
  if weight-1 > 0.5:
    pair r1, r2, r3, r4, r5, r8;
    pen eraser;
    r1 = (1, 0) rotated -20 scaled ((0.131 + 0.093 * (weight-1)) * 0.5 * h) shifted begin;
    r2 = (0, -1+(0.25*(weight-1))) scaled ((0.131 + 0.093 * (weight-1)) * 0.5 * h) shifted upper;
    r3 = (-1, 0) scaled ((0.131 + 0.093 * (weight-1)) * 0.5 * h) shifted back;
    r5 = (0, 1.2) scaled ((0.131 + 0.093 * (weight-1)) * 0.5 * h) shifted basis;
    r4 = 0.5[r3, r5] + (0.13*w, 0);
    r8 = r4 + (0.14*w, 0.1*h);
    eraser = makepen(r1..r2..r3..r8..r4--r5..cycle);
    pickup eraser;
    erase (0,0);
  fi
endchar;

beginchar("3", (0.475+0.047*(weight-1)+0.035*italic+0.017*italic*(weight-1)) * size * pt, 0.68*size*pt, 0);
  pair p1, p2, p3, p4, p6, p7, p8;
  path semicircle;
  pen eraser;
  % Preparing the eraser
  semicircle =   (0.5, 0)
  .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
  (0.35356, 0.35356)
  .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
  (0, 0.5);
  semicircle = semicircle & semicircle rotated 90 -- (0.5, 0) & cycle;
  eraser = makepen(semicircle);
  % Upper part of '3':
  pickup pencircle xscaled ((0.127+0.096*(weight-1))*h) yscaled ((0.101+0.062*(weight-1))*h);
  p1 = ((0.104+0.04*(weight-1)+0.086*italic)*w, 0.756*h);
  p2 = bot ((0.5+0.077*italic)*w, h);
  p3 = lft ((0.96+0.025*italic)*w, (0.754+0.015*italic)*h);
  p4 = bot (0.373*w, (0.554+0.037*(weight-1))*h);
  draw p1..{1, 0}p2..p3..{-1, 0}p4;
  % Lower part of '3':
  p6 = lft ((1-0.065*italic)*w, 0.289*h);
  p7 = top (0.487*w, 0);
  p8 = rt (0, 0.253*h);
  draw p4{1, 0}..{0, -1}p6..{-1, 0}p7..p8;
  % Erasing:
  pickup eraser xscaled ((0.13+0.1*(weight-1))*h) yscaled ((0.11+0.1*(weight-1))*h) rotated -196.6;
  erase p1;
  pickup eraser xscaled ((0.14+0.1*(weight-1))*h) yscaled ((0.12+0.1*(weight-1))*h) rotated 16.6;
  erase p8;
  pickup eraser xscaled ((0.14+0.1*(weight-1))*h) yscaled ((0.13+0.1*(weight-1))*h) rotated (90-12*italic);
  erase p4;
endchar;
beginchar("4", (0.54+0.03*(weight-1)+0.03*italic+0.017*italic*(weight-1))*size*pt, 0.667*size*pt, 0);
  pair p1, p2, p3, p4, p5, p6, p7, p8, p9, p10;
  pen four1, four2, four3;
  numeric ic;
  pickup pensquare xscaled ((0.126+0.102*(weight-1))*h);
  p1 = bot rt ((0.617-0.046*(weight-1)-0.080*italic)* w, 0);
  p2 = (xpart p1 + 0.247*italic*w, h);
  draw p1--p2; % Horizontal line
  pickup pensquare yscaled ((0.117+0.085*(weight-1))*h) rotated (-12.14*italic);
  ic = ((0.117+0.085*(weight-1))*h) * sind(12.4*italic);
  p3 = (0, (0.365+0.045*(weight-1))*h);
  p4 = lft (w, ypart p3);
  draw rt bot p3-- bot p4; % Vertical line
  pickup pensquare xscaled ((0.13+0.11*(weight-1))*h);
  draw p2-- rt (xpart p3 + ic, ypart p3); 
endchar;
beginchar("5", (0.47+0.055*(weight-1)+0.07*italic-0.023*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  pair p1, p2, p3, p4, p5, p6, p7, p8;
  pickup pensquare slanted (0.25*italic-0.02*italic*(weight-1)) yscaled ((0.119+0.063*(weight-1))*h) xscaled ((0.113+0.066*(weight-1))*h);
  p1 = bot lft((0.946+0.054*italic)*w, h);
  p2 = bot rt ((0.126-0.048*(weight-1)+0.055*italic)*h, h);
  p3 = bot rt ((0.039-0.039*(weight-1)+0.021*italic-0.025*italic*(weight-1))*w, (0.5-0.05*(weight-1)-0.05*italic+0.05*italic*(weight-1))*h);
  draw p1--p2--p3;
  pickup pensquare yscaled ((0.119+0.063*(weight-1))*h) xscaled w;
  erase p3;
  pickup pencircle yscaled ((0.133+0.039*(weight-1)-0.01*italic)*h) xscaled ((0.133+0.096*(weight-1)-0.01*italic-0.05*italic*(weight-1))*h);
  p4 = top (xpart p3 + 0.02*w, ypart p3);
  p5 = bot ((0.529-0.016*italic)*w, 0.679*h);
  p6 = lft ((1-0.112*italic)*w, 0.341*h);
  p7 = top ((0.493-0.071*italic)*w, 0);
  p8 = rt (0, 0.24*h);
  draw p4..{1, 0}p5..{0, -1}p6..{-1, 0}p7..p8;
  % Erasing:
  path semicircle;
  pen eraser;
  semicircle =   (0.5, 0)
  .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
  (0.35356, 0.35356)
  .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
  (0, 0.5);
  semicircle = semicircle & semicircle rotated 90 -- (0.5, 0) & cycle;
  eraser = makepen(semicircle);
  pickup eraser scaled ((0.18+0.07*(weight-1))*h) rotated (-196-3*(weight-1));
  erase p4;
  pickup eraser scaled ((0.18+(0.07*(weight-1)))*h) rotated 16;
  erase p8;
endchar;
beginchar("6", (0.483+0.05*(weight-1)+0.03*italic-0.017*italic*(weight-1))*size*pt, 0.68*size*pt, 0);
  pair p1, pp1, p2, p3, p4, p5, p6, p7;
  path circ;
  pickup pencircle xscaled ((0.126+0.088*(weight-1))*h) yscaled ((0.101+0.058*(weight-1))*h);
  p1 = ((0.849-0.051*(weight-1))*w, 0.827*h);
  pp1 = lft (w, 0.827*h);
  p1 = italic[p1, pp1]; 
  p2 = bot ((0.546-0.003*(weight-1)+0.085*italic)*w, h);
  p3 = rt (0, 0.465*h);
  p4 = (0.015*w, 0.332*h);
  p5 = ((0.508-0.062*italic)*w, 0);
  p6 = ((1-0.041*italic)*w, 0.332*h);
  draw p1..{-1, 0}p2..{0, -1}p3..(xpart rt p4, ypart p4 - 0.05*h);
  pickup pencircle xscaled ((0.13+0.095*(weight-1))*h) yscaled ((0.1+0.065*(weight-1))*h);
  p7 = bot ((0.55+0.01*italic)*w, 0.66*h);
  circ = rt p4 .. {1, 0}(top p5)..{0, 1}lft p6..{-1, 0}p7..cycle;
  draw circ;
  % Erasing:
  path semicircle;
  pen eraser;
  semicircle =   (0.5, 0)
  .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
  (0.35356, 0.35356)
  .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
  (0, 0.5);
  semicircle = semicircle & semicircle rotated 90 -- (0.5, 0) & cycle;
  eraser = makepen(semicircle);
  pickup eraser xscaled ((0.13+0.09*(weight-1))*h) yscaled ((0.11+0.08*(weight-1))*h) rotated 197;
  erase p1;
endchar;
beginchar("7", (0.465+0.037*(weight-1)+0.073*italic+0.007*italic*(weight-1))*size*pt, 0.667*size*pt, 0);
  pair p1, p2, p3, pp3;
  pickup pensquare slanted (0.25*italic) yscaled ((0.121+0.065*(weight-1))*h) xscaled ((0.09+0.06*(weight-1))*h);
  p1 = bot rt (0.088*italic*w, h);
  p2 = bot lft (w, h);
  p3 = bot rt ((0.169-0.033*(weight-1)-0.2*italic+0.02*italic*(weight-1))*w, 0);
  draw p1--p2--p3;
endchar;
beginchar("8", (0.497+0.054*(weight-1)+0.024*italic-0.013*italic*(weight-1))*size*pt, 0.68*size*pt, 0);
  path circle, upper, lower;
  numeric udiameter, pendiameter, lowermult;
  circle =
    (0.5, 0)
    .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
    (0.35356, 0.35356)
    .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
    (0, 0.5);
  udiameter = (0.42-0.03*(weight-1))*h;
  pendiameter = (0.108+0.078*(weight-1))*h;
  lowermult = (1.2+0.1*(weight-1));
  circle = circle & circle rotated 90;
  circle = circle & circle rotated 180 & cycle;
  upper = circle scaled udiameter shifted (0.5*w, h-((udiameter+pendiameter)/2)) slanted(0.08*italic);
  pickup pencircle scaled pendiameter;
  draw upper;
  pickup pencircle yscaled pendiameter xscaled (pendiameter*lowermult);
  lower = circle xscaled ((w-(pendiameter*lowermult))*(1-0.05*italic)) yscaled (h-udiameter-pendiameter);
  lower = lower shifted (0.5*w, (h-udiameter)/2);
  draw lower;
endchar;
beginchar("9", (0.483-0.002*(weight-1)+0.103*italic+0.03*italic*(weight-1))*size*pt, 0.68*size*pt, 0);
  path circle;
  numeric basis, pendiameter, slant, circle_diameter;
  pair circle_connection, circle_center;
  basis = (0.32-0.044*(weight-1)-0.15*italic)*w;
  slant = 0.1 * italic;
  pendiameter = (0.12+0.08*(weight-1)-0.00*italic)*h;
  circle_diameter = (w-pendiameter)*(1-0.25*italic);
  circle_center = (0.5*w, h-(circle_diameter)/2-pendiameter/2);
  circle = 
    (0.5, 0)
    .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
    (0.35356, 0.35356)
    .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
    (0, 0.5);
  circle = circle & circle rotated 90;
  circle = circle & circle rotated 180 & cycle;
  pickup pencircle scaled pendiameter;
  circle = circle slanted slant scaled circle_diameter shifted circle_center;
  draw circle;
  circle_connection = (cosd (-20-10*italic), sind (-20-10*italic)) * (circle_diameter/2);
  circle_connection = circle_connection slanted slant shifted circle_center;
  draw circle_connection  -- bot rt (basis, 0);
  pickup pencircle slanted slant scaled (circle_diameter-pendiameter);
  erase circle_center;
endchar;

beginchar(":", (0.13+0.067*(weight-1)+0.082*italic-0.012*italic*(weight-1))*size*pt, (0.505+0.005*(weight-1))*size*pt, 0);
  numeric pen_diameter;
  pair top1, top2, bot1, bot2;
  pen_diameter = w;
  if pen_diameter > (0.25+0.13*(weight-1)) * h:
    pen_diameter = (0.25+0.13*(weight-1)) * h;
  fi
  pickup pencircle scaled pen_diameter;
  top1 = bot (0.5*w, h);
  top2 = bot lft (w, h);
  bot1 = top (0.5*w, 0);
  bot2 = top rt (0, 0);
  draw italic[top1, top2];
  draw italic[bot1, bot2];
endchar;

beginchar(";", (0.13+0.067*(weight-1)+0.117*italic-0.017*italic*(weight-1))*size*pt, (0.505+0.005*(weight-1))*size*pt, (0.132+0.015*(weight-1))*size*pt);
  numeric pen_diameter;
  pair top1, top2, bot1, bot2;
  pair bottom, circle_connection, bottom_point;
  pen_diameter = w;
  if pen_diameter > (0.25+0.13*(weight-1)) * h:
    pen_diameter = (0.25+0.13*(weight-1)) * h;
  fi
  pickup pencircle scaled pen_diameter;
  top1 = bot (0.5*w, h);
  top2 = bot lft (w, h);
  bot1 = top (0.5*w, 0);
  bot2 = top rt (0.1*italic*w, 0);
  bottom_point = italic[bot1, bot2];
  draw italic[top1, top2];
  draw bottom_point;
  pickup pencircle scaled (pen_diameter*0.4);
  bottom = rt ((0.1*(weight-1)-0.1*italic*(weight-1))*w, -d);
  circle_connection = (cosd (-20+10*(weight-1)*italic), sind (-20+10*(weight-1)*italic)) * (pen_diameter/2);
  circle_connection = circle_connection shifted (lft bottom_point);
  draw circle_connection -- bottom;
endchar;

beginchar("<", (0.478+0.02*(weight-1)-0.002*italic)*size*pt, (0.495+0.015*(weight-1))*size*pt, 0);
  pickup pencircle scaled ((0.17+0.06*(weight-1)) * h);
  draw top (w, 0) -- (0, 0.5*h) -- bot (w, h);
endchar;

beginchar("=", (0.497+0.016*(weight-1)+0.001*italic*(weight-1))*size*pt, (0.387+0.022*(weight-1))*size*pt, -((0.12-0.025*(weight-1))*size*pt));
  pickup pencircle scaled ((0.205+0.105*(weight-1)) * h);
  draw bot (0, h) -- bot (w, h);
  draw top (0, -d) -- top (w, -d);
endchar;

beginchar(">", (0.478+0.02*(weight-1)-0.002*italic)*size*pt, (0.495+0.015*(weight-1))*size*pt, 0);
  pickup pencircle scaled ((0.17+0.06*(weight-1)) * h);
  draw top (0, 0) -- (w, 0.5*h) -- bot (0, h);
endchar;

beginchar("?", (0.432+0.095*(weight-1)+0.02*italic+0.015*italic*(weight-1))*size*pt, 0.68*size*pt, 0);
  pickup pencircle yscaled (0.101+0.075*(weight-1))*h xscaled (0.127+0.098*(weight-1))*h;
  pair p1, p2, p3, p8, p9;
  p1 = rt (0, 0.759*h); % Beginning: position to the right
  p2 = (0.514*w, h); % Top of '?'
  p3 = ((1-0.05*italic)*w, 0.763*h);
  p8 = ((0.43-0.05*italic)*w, (0.347+0.02*(weight-1)+0.1*italic)*h);
  p9 = ((0.43-0.15*italic)*w, (0.292+0.075*(weight-1))*h);
  draw p1..{1, 0}bot p2..{0, -1}lft p3..{0, -1+1*italic}p8..p9;
  % Erasing
  path semicircle;
  pen eraser;
  semicircle =   (0.5, 0)
    .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
    (0.35356, 0.35356)
    .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
    (0, 0.5);
  semicircle = semicircle & semicircle rotated 90 -- (0.5, 0) & cycle;
  eraser = makepen(semicircle);
  pickup eraser yscaled ((0.11+0.09*(weight-1))*h) xscaled ((0.13+0.1*(weight-1))*h)  rotated (-196-3*(weight-1));  
  erase p1;
  pickup eraser yscaled ((0.11+0.09*(weight-1))*h) xscaled ((0.13+0.15*(weight-1))*h) rotated 180;
  erase p9;
  % Drawing lower point
  pickup pencircle scaled (0.186+0.096*(weight-1))*h;
  draw top ((0.43-0.2*italic)*w, 0);
endchar;

beginchar("@", (0.682+0.006*(weight-1))*size*pt, 0.68*size*pt, 0);
  pair p1, p2, p3, p5, p4, p7, p8, p9, p10, p11, p12, p6;
  pickup pencircle yscaled ((0.107+0.025*(weight-1))*h) xscaled ((0.117+0.019*(weight-1))*h);
  p1 = ((0.65-0.026*(weight-1)+0.017*italic)*w, (0.694-0.012*(weight-1)+0.004*italic)*h);
  p2 = ((0.31-0.032*italic)*w, (0.458+0.029*(weight-1)-0.032*italic)*h);
  p3 = ((0.456-0.057*italic)*w, (0.279+0.035*(weight-1)+0.017*italic)*h);
  p4=((0.554-0.016*(weight-1)-0.053*italic)*w, (0.327+0.015*(weight-1)+0.004*italic)*h);
  p5 = (-0.04*w*italic + xpart p1, (0.487-0.009*italic)*h);
  draw p1..{0, -1}p2..{1, 0}p3.. tension (1+0.5*italic) .. p4 .. p5--p1;
  pickup pencircle yscaled (0.107*h) xscaled (0.117*h);
  p6 = ((0.005*(weight-1)*w) + xpart p5, ypart p5); % BUG: Seg fault
  p7 = (0.774*w, 0.296*h);
  p8 = lft (w, 0.546*h);
  p9 = bot (0.509*w, h);
  p10 = rt (0, 0.501*h);
  p11 = top (0.512*w, 0);
  p12 = (0.709*w, 0.091*h);
  draw p6{-0.174+0.174*italic, -1+0.015*italic}..{1, 0}p7..{0, 1}p8..{-1, 0}p9..{0, -1}p10..{1, 0}p11..p12;
  % Retouch:
  pickup pensquare scaled (0.117.h);
  erase rt p12;
  if italic > 0.5:
    pen eraser;
    pair r0, r1, r2, r3;
    r1 = (-0.25*(weight-1), 1+(0.2*(weight-1))) yscaled ((0.0535+0.0125*(weight-1))*h) xscaled ((0.117+0.019*(weight-1))*h) shifted p3;
    r3 = (-1, 0) xscaled ((0.0585+0.0095*(weight-1))*h) shifted p5;
    r0 = (-1.1, -0.7) yscaled ((0.0535+0.0125*(weight-1))*h) xscaled ((0.0585+0.0095*(weight-1))*h) shifted p1;
    r2 = 0.5[r1, r3] + ((0.04+0.015*(weight-1))*w, -0.03-0.015*(weight-1)*h);
    eraser = makepen(r1..r2..r3..r0--cycle);
    pickup eraser;
    erase (0, 0);
  fi
endchar;

beginchar("A", (0.608+0.075*(weight-1)+0.01*italic-0.003*italic*(weight-1))*size*pt, (0.67-0.002*(weight-1)-0.002*italic+0.002*italic*(weight-1))*size*pt, 0);
  numeric pen_width, pen_height;
  pen_width = ((0.13+0.127*(weight-1))*h);
  pen_height = ((0.121+0.071*(weight-1))*h);
  pickup pencircle xscaled pen_width yscaled pen_height;
  pair p1, p2, p3, p4, p5;
  numeric basis, slant;
  basis = 0.277-0.01*(weight-1);
  slant = 0.5+0.229*italic;
  p1 = rt (0, 0);
  p2 = (slant*w, h);
  p3 = lft (w, 0);
  p4 = basis[p1, p2];
  p5 = basis[p3, p2];
  draw p1--p2--p3;
  pickup pencircle xscaled pen_width yscaled pen_height;
  draw p4--p5;
  pair t1, t2, t3;
  pen triangle_pen;
  t1 = ((xpart p4)+pen_width/2+0.03*w, (ypart p4)+pen_height/2);
  t2 = ((xpart p5)-pen_width/2-0.03*w, (ypart p5)+pen_height/2);
  t3 = ((slant-0.04*italic-0.042*italic*(weight-1))*w, (0.84-0.1*(weight-1))*h);
  triangle_pen = makepen(t1--t2--t3--cycle);
  pickup triangle_pen;
  erase (0, 0);
endchar;

beginchar("B", (0.506+0.07*(weight-1)+0.138*italic-0.039*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  pickup pencircle xscaled ((0.141+0.096*(weight-1))*h) yscaled ((0.118+0.078*(weight-1))*h);
  numeric half;
  pair p1, p2, p3, p4, p5, p6, p7, p8, p9, p10;
  p1 = top rt (0.23*w*italic, h);
  p2 = bot rt (-0.012*w*italic-0.008*w*italic*(weight-1), 0);
  draw p1--p2;
  p3 = top rt (0.015*w*italic+0.01*w*italic*(weight-1), 0);
  p4 = top ((0.56-0.09*italic)*w, 0);
  draw p3 -- p4;
  p5 = (0.105+(weight-1)*0.059)[p1, p2];
  p6 = bot ((0.56+0.06*italic)*w, h);
  draw p5--p6;
  half = 0.49;
  p7 = half[p1, p2];
  p8 = (0.56*w, ypart p7);
  draw p7--p8;
  p9 = lft ((1-italic*0.13+0.04*italic*(weight-1))*w, 0.287*h);
  if italic < 0.5:
    draw p4{1, 0}..p9{0, 1}..{-1, 0}p8;
  else:
    draw p4{1, 0}..p9..{-1, 0}p8;
  fi
  p10 = lft ((0.943-0.03*italic+0.045*italic*(weight-1))*w, 0.751*h);
  draw p8{1, 0}..p10..{-1, 0}p6;
endchar;

beginchar("C", (0.57+0.048*(weight-1)+0.054*italic-0.004*italic*(weight-1))*size*pt, 0.68*size*pt, 0);
  pickup pencircle xscaled ((0.139+0.102*(weight-1))*h) yscaled ((0.114+0.074*(weight-1))*h);
  pair p1, p2, p3, p4, p5;
  p1 = lft ((0.993+0.007*italic)*w, 0.762*h);
  p2 = bot ((0.548+0.063*italic)*w, h);
  p3 = rt (0.03*w*italic, (0.501-0.102*italic)*h);
  p4 = top ((0.533-0.098*italic)*w, 0);
  p5 = lft ((1-0.1*italic)*w, 0.238*h);
  draw p1..{-1, 0}p2..{0, -1}p3..{1, 0}p4..p5;
  % Erasing
  pickup pensemicircle rotated (200) xscaled ((0.149+0.112*(weight-1))*h) yscaled ((0.124+0.074*(weight-1))*h);
  erase p1;
  pickup pensemicircle  rotated (-20) xscaled ((0.149+0.101*(weight-1))*h) yscaled ((0.124+0.074*(weight-1))*h);
  erase p5;
endchar;
  

beginchar("D", (0.55+0.06*(weight-1)+0.112*italic-0.007*italic*(weight-1))*size*pt, 0.667*size*pt, 0);
  numeric pen_width, pen_height;
  pen_width = (0.141+0.096*(weight-1))*h;
  pen_height = (0.118+0.078*(weight-1))*h;
  pickup pencircle xscaled pen_width yscaled pen_height;
  pair p1, p2;
  p1 = rt (0, 0);
  p2 = rt (0.227*italic*w, h);
  draw p1--p2;
  pair pp1, pp2, p3, p4;
  pp1 = (pen_height/(2*h))[p1, p2];
  pp2 = ((2*h-pen_height)/(2*h))[p1, p2];
  p3 = top ((0.331+0.035*(weight-1)-0.035*italic)*w, 0);
  p4 = bot ((0.331+0.035*(weight-1)+0.191*italic)*w, h);
  draw pp1--p3;
  draw pp2--p4;
  pair pp4, pp3, p5;
  pp3 = ((0.654+0.046*(weight-1)-0.033*italic)*w, (0.106+0.068*(weight-1)+0.009*italic)*h);
  pp4 = ((0.654+0.046*(weight-1))*w, (0.894-0.068*(weight-1))*h);
  p5 = lft ((1.0-0.05*italic)*w, (0.501+0.116*italic)*h);
  if(italic > 0.3):
    draw p4{1, 0}..{0, -1}p5..pp3..{-1, 0}p3;
  else:
    draw p4{1, 0}..pp4..{0, -1}p5..pp3..{-1, 0}p3;
  fi
endchar;

beginchar("E", (0.455+0.06*(weight-1)+0.144*italic+0.002*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  numeric pen_size;
  pair p1, p2;
  pen_size = ((0.141+0.096*(weight-1))*h);
  p1 = (0, 0);
  p2 = (0.238*w*italic, h);
  pickup pencircle scaled pen_size;
  draw rt p1--rt p2;
  pickup pensquare scaled (pen_size*0.87) slanted (italic*0.25);
  pair p3;
  p3 = top lft ((1-0.209*italic)*w, 0);
  draw top rt p1--p3;
  draw bot rt p2--bot lft (w, h);
  pair p4;
  p4 = lft ((0.781-0.051*(weight-1)-0.05*italic+0.003*italic*(weight-1))*w, 0.515*h);
  draw (0.515[rt p1, rt p2]) -- p4;
endchar;

beginchar("F", (0.435+0.045*(weight-1)+0.144*italic+0.001*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  numeric pen_size;
  pair p1, p2;
  pen_size = ((0.141+0.096*(weight-1))*h);
  p1 = (0, 0);
  p2 = (0.238*w*italic, h);
  pickup pencircle scaled pen_size;
  draw rt p1--rt p2;
  pickup pensquare scaled (pen_size*0.87) slanted (italic*0.25);
  draw bot rt p2--bot lft (w, h);
  pair p4;
  p4 = lft ((1.0-0.1*italic+0.003*italic*(weight-1))*w, 0.515*h);
  draw (0.515[rt p1, rt p2]) -- p4;
endchar;

beginchar("G", (0.595+0.055*(weight-1)+0.032*italic+0.018*italic*(weight-1))*size*pt, 0.681*size*pt, 0);
  numeric pen_height, pen_width;
  pen_width = (0.139+0.102*(weight-1))*h;
  pen_height = (0.114+0.074*(weight-1))*h;
  pickup pencircle xscaled pen_width yscaled pen_height;
  pair p1, p2, p3, p4, p5, p6;
  p1 = ((0.883-0.043*(weight-1))*w, 0.76*h);
  p1 = italic[p1, lft ((1.0-0.03*(weight-1))*w, ypart p1)];
  p2 = bot ((0.525-0.034*(weight-1)+0.093*italic+0.015*italic*(weight-1))*w, h);
  p3 = rt (0.03*w*italic, (0.501-0.096*italic)*h);
  p4 = top ((0.51-0.021*(weight-1)-0.083*italic-0.011*(weight-1)*italic)*w, 0);
  p5 = ((0.883-0.029*(weight-1))*w, (0.457+0.013*(weight-1))*h);
  p6 = ((0.52+0.003*italic)*w, (0.457+0.013*(weight-1))*h);
  draw p1..{-1, 0}p2..{0, -1}p3..{1, 0}p4..tension (0.85-0.02*(weight-1)*italic)..{0, 1}p5--p6;
  % Retouch:
  pickup pensemicircle rotated (199.26) xscaled (pen_width*1.1) yscaled (pen_height*1.1);
  erase p1;
  pickup pensemicircle rotated (90-11*italic) xscaled (pen_width) yscaled (pen_height);
  erase p6;
  pen in;
  % Serif:
  pair s1, s2, s3, s4, s5;
  s1 = (xpart p5 - pen_width/2, ypart p5 + pen_height/2);
  s2 = (w, ypart s1);
  s3 = ((1-0.121*italic)*w, 0);
  s4 = (xpart s3 - (0.09+0.07*(weight-1)-0.004*(weight-1)*italic)*w, 0);
  s5 = ((0.867-0.061*(weight-1)-0.086*italic)*w, (0.140-0.014*(weight-1))*h);
  in = makepen(s1--s2--s3--s4--s5--cycle);
  pickup in;
  draw (0, 0);
  in = makepen(s2--(w, 0)--s3--cycle);
  pickup in;
  erase (0, 0);
  % More retouch
  path i;
  pair r1, r2, r3, r4, r5;
  r1 = (xpart p6, ypart p6 - pen_height/2);
  r2 = (xpart p5-pen_width/2, ypart p5 - pen_height/2);
  r3 = ((0.517+0.009*(weight-1)-0.044*italic-0.053*italic*(weight-1))*w, pen_height-(italic*0.01*h));
  r4 = ((0.275+0.058*(weight-1)-0.002*italic-0.003*italic*(weight-1))*w, (0.215+0.057*(weight-1)-0.029*italic+0.005*italic*(weight-1))*h);
  r5 = (xpart r1, ypart r4);
  if italic*(weight-1) < 0.5:
    i = r1--r2..{-1, 0}r3..r4--cycle;
  else:
    i = r1--r2{-0.25, -1}..{-1, 0}r3..r4--cycle;
  fi
  in = makepen(i);
  pickup in;
  erase (0, 0);
endchar;

beginchar("H", (0.54+0.053*(weight-1)+0.146*italic+0.004*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  pickup pencircle xscaled ((0.141+0.096*(weight-1))*h) yscaled ((0.121+0.087*(weight-1))*h);
  pair p1, p2;
  p1 = rt (0, 0);
  p2 = rt (0.208*w*italic, h);
  draw p1--p2;
  pair p3, p4;
  p3 = lft ((1-0.208*italic)*w, 0);
  p4 = lft (w, h);
  draw p3--p4;
  pair p5, p6;
  p5 = 0.514[p1, p2];
  p6 = 0.514[p3, p4];
  draw p5--p6;
endchar;

beginchar("I", (0.283+0.062*(weight-1)+0.141*italic+0.002*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  pickup pencircle scaled ((0.141+0.096*(weight-1))*h);
  pair p1, p2;
  p1 = ((0.5-0.149*italic)*w, 0);
  p2 = ((0.5+0.149*italic)*w, h);
  draw p1--p2;
  pickup pensquare slanted (italic*0.2) scaled ((0.111+0.076*(weight-1))*h);
  draw top rt (0, 0) -- top lft ((1-0.298*italic+0.06*italic*(weight-1))*w, 0);
  draw bot rt ((0.298*italic-0.06*italic*(weight-1))*w, h) -- bot lft (w, h);
endchar;

beginchar("J", (0.422+0.092*(weight-1)+0.115*italic-0.012*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  numeric pen_width, pen_height;
  pen_width = (0.138+0.094*(weight-1))*h;
  pen_height = (0.113+0.071*(weight-1))*h;
  pickup pencircle xscaled pen_width yscaled pen_height;
  pair p1, p2, p3, p4;
  p1 = lft (w, h);
  p2 = lft ((1-0.184*italic+0.025*italic*(weight-1))*w, 0.329*h);
  p3 = top ((0.499-0.01*(weight-1)-0.151*italic)*w, 0);
  p4 = rt ((0.01*italic-0.01*italic*(weight-1))*w, 0.313*h);
  numeric ang, final_ang;
  ang = angle(p2-p1);
  final_ang = 95-15*italic+20*italic*(weight-1);
  draw p1..p2{cosd(ang), -sind(ang)}..{-1, 0}p3..p4;
  pickup pensemicircle rotated 20 xscaled (pen_width*1.2) yscaled (pen_height*1.2);
  erase p4;
  % Retouch:
  pair r1, r2, r3;
  pen eraser;
  r1 = (0, 1) xscaled pen_width yscaled 0.5*pen_height shifted p3;
  r2 = (1, 0) xscaled 0.5*pen_width yscaled pen_height rotated 20 shifted p4;
  r2 = ((length (r1-p3))/(length (r2-p4)), 0) xscaled 0.5*pen_width yscaled pen_height rotated 20 shifted p4;
  r3 = (xpart r1, ypart r2);
  eraser = makepen(r1{-1, 0}..{0, 1}r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
endchar;

beginchar("K", (0.543+0.065*(weight-1)+0.115*italic+0.017*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  pickup pencircle scaled ((0.141+0.096*(weight-1))*h);
  pair p1, p2;
  p1 = rt (0, 0);
  p2 = rt ((0.216*italic)*w, h);
  draw p1--p2;
  pickup pencircle scaled ((0.141+0.07*(weight-1))*h);
  pair p3, p4;
  p3 = (0.371-0.02*italic)[p1, p2];
  p4 = lft ((0.907+0.02*(weight-1)+0.06*italic-0.04*italic*(weight-1))*w, h);
  draw p3--(p4 + 0.1*(p4-p3));
  pair p5, p6;
  p5 = 0.349[p3, p4];
  p6 = lft ((0.98-0.01*(weight-1)-0.173*italic)*w, 0);
  draw p5--(p6 + 0.1*(p6-p5));
endchar;

beginchar("L", (0.455+0.025*(weight-1)+0.02*italic+0.013*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  pair p1, p2, p3;
  numeric slt, scaleX, scaleY, ang;
  slt = 0.2*italic;
  scaleX = (0.141+0.096*(weight-1))*h;
  scaleY = (0.121+0.087*(weight-1))*h;
  ang = 90 - angle(((0.5, 0.5) slanted slt yscaled scaleY xscaled scaleX)-
      ((0.5, -0.5) slanted slt yscaled scaleY xscaled scaleX));
  pickup pensquare slanted slt yscaled scaleY xscaled scaleX;
  p1 = rt top (0, 0);
  p2 = rt ((h-scaleY)*sind(ang), h-0.5*scaleY);
  p3 = lft top (w, 0);
  draw p2--p1--p3;
endchar;

beginchar("M", (0.67+0.085*(weight-1)+0.145*italic+0.005*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  numeric length1, length2;
  pair p1, p2, p3, p4;
  length1 = (0.141+0.088*(weight-1))*h;
  length2 = (0.139+0.056*(weight-1))*h;
  pickup pencircle scaled length1;
  p1 = (0.5*length1, 0);
  p2 = (0.5*length1 + 0.213*italic*w, h);
  p3 = (w-0.5*length1-0.174*italic*w, 0);
  p4 = (w-0.5*length1, h);
  draw p1--p2;
  draw p3--p4;
  pair p5, p6, p8;
  p5 = (xpart p2 + 0.5*length1 + 0.2 * length2, h);
  p8 = (xpart p4 - 0.5*length1 - 0.2 * length2, h);
  p6 = ((0.5-0.085*italic)*w, 0);
  numeric ang, slt;
  ang = 90+angle(p5-p6);
  slt = sind(ang);
  pickup pensquare slanted slt scaled length2;
  draw p5--p6;
  ang = 90+angle(p8-p6);
  slt = sind(ang);
  pickup pensquare slanted slt scaled length2;
  draw p6--p8;
endchar;

beginchar("N", (0.538+0.075*(weight-1)+0.146*italic+0.005*italic*(weight-1))*size*pt, 0.668*size*pt, 0);
  numeric length1, length2;
  pair p1, p2, p3, p4;
  length1 = (0.141+0.088*(weight-1))*h;
  length2 = (0.139+0.056*(weight-1))*h;
  pickup pencircle scaled length1;
  p1 = (0.5*length1, 0);
  p2 = (0.5*length1 + 0.213*italic*w, h);
  p3 = (w-0.5*length1-0.174*italic*w, 0);
  p4 = (w-0.5*length1, h);
  draw p1--p2;
  draw p3--p4;
  pair p5, p8;
  p5 = (xpart p2 + 0.3*length1 + 0.0 * length2, h);
  p8 = (xpart p3 - 0.3*length1 - 0.0 * length2, 0);
  numeric ang, slt;
  ang = 90+angle(p5-p8);
  slt = sind(ang);
  pickup pensquare slanted slt scaled length2;
  draw p5--p8;
endchar;

beginchar("O", (0.63+0.065*(weight-1)+0.058*italic-0.009*italic*(weight-1))*size*pt, 0.68*size*pt, 0);
  pickup pencircle xscaled ((0.139+0.102*(weight-1))*h) yscaled ((0.114+0.074*(weight-1))*h);
  pair p1, p2, p3, p4;
  p1 = bot ((0.5+0.086*italic)*w, h);
  p2 = rt (0.0385*italic*w, (0.5-0.094*italic)*h);
  p3 = top ((0.5-0.088*italic)*w, 0);
  p4 = lft ((1-0.0385*italic)*w, (0.5+0.1*italic)*h);
  draw p1{-1, 0}..p2{0, -1}..p3{1, 0}..p4{0, 1}..cycle;
endchar;

beginchar("P", (0.489+0.072*(weight-1)+0.114*italic+0.003*italic*(weight-1))*size*pt, 0.667*size*pt, 0);
  % PARAMETERS:
  numeric pen_width, pen_height, ratio, ang;
  pen_width = ((0.137+0.1*(weight-1))*h);
  pen_height = ((0.13+0.07*(weight-1))*h);
  ratio = (0.465-0.017*(weight-1)); % Ratio between 'P' leg and leg+head
  ang = 12*italic;
  % CONTROL POINTS
  pickup pencircle xscaled pen_width yscaled pen_height;
  pair p1, p2, p2b, p3, p4, p5, p6;
  p1 = (pen_width/2, 0);    % 'P' bottom point
  p2 = (pen_width/2  + h * sind ang, h);    % 'P' upper left point
  p2b = (1.0-0.5*(pen_height/h))[p1, p2];
  p3 = (0.6*w, h-(pen_height/2)); % uppermost rightmost point in horizontal line
  p4 = ratio[p1, p2];       % Point where lower part of 'P' head split from leg
  p5 = (0.6*w, ratio*h); % lower rightmost point in horizontal line
  p6 = lft ((1-0.03*italic)*w, ypart(0.5[p5, p3]));
  draw p1--p2;
  draw bot p2 -- p3;
  draw p4 -- p5;
  draw p5{1, 0}..{0, 1}p6..{-1, 0}p3;
endchar;

beginchar("Q", (0.703+0.044*(weight-1)-0.018*italic+0.015*italic*(weight-1))*size*pt, (0.68+0.016*italic+0.01*(weight-1)*italic)*size*pt, (0.045+0.01*(weight-1)+0.01*italic-0.01*italic*(weight-1))*size*pt);
  % PARAMETERS:
  numeric pen_width, ratio;
  pen_width = ((0.139+0.04*(weight-1))*h);
  ratio = 0.89+0.04*(weight-1)+0.1*italic-0.025*(weight-1)*italic;
  % CONTROL POINTS:
  pair p1, p2;
  p1 = italic[((ratio*w)/2, h/2),
              ((0.95*ratio*w)/2, (0.9*h)/2)];
  p2 = italic[(w-0.3535*pen_width, -d+0.3535*pen_width),
              (0.95*ratio*w-0.3535*pen_width, -d+0.3535*pen_width)];
  % RENDERING:
  pickup pencircle scaled pen_width;
  renderchar "O" between (0, 0) and (ratio*w, h);
  draw p1--p2;
  pickup pensemicircle scaled pen_width rotated 45;
  erase p1;
  pickup pensemicircle scaled pen_width rotated 225;
  erase p2;
endchar;

beginchar("R", (0.513+0.081*(weight-1)+0.092*italic-0.017*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS:
  numeric ratio, ratio_leg, pen_width, ratio_leg2;
  ratio = 0.95 - 0.01*(weight-1) + 0.04*italic + 0.02*italic*(weight-1);
  ratio_leg = (0.465-0.017*(weight-1)); % Ratio between 'P' leg and leg+head
  ratio_leg2 = (0.45+0.035*(weight-1)) * ratio;
  pen_width = ((0.137+0.13*(weight-1))*h);
  % CONTROL POINTS
  pair p1, p2;
  p1 = (ratio_leg2*w, ratio_leg*h);
  p2 = (w - pen_width/2 - 0.17*italic*w + 0.04*italic*(weight-1)*w, 0);
  % RENDERING:
  pickup pensquare xscaled pen_width;
  renderchar "P" between (0, 0) and (ratio*w, h);
  draw p1--p2;
endchar;

beginchar("S", (0.517+0.057*(weight-1)+0.01*italic+0.015*italic*(weight-1))*size*pt,
    0.6575*size*pt, 0);
  pair p0, p1, p2, p3, p4, p5, p6;
  path s;
  transform t;
  t = identity xscaled w yscaled (h+d);
  pickup pencircle xscaled ((0.177+0.106*(weight-1)) * w) yscaled ((0.11+0.05*(weight-1)) * (h+d));
  p1 = top (0.5*w, 0);
  p5 = bot (w/2, h);
  p0 = rt (0, ypart p1 + 0.194 * (ypart p5 - ypart p1));
  p2 = lft ((1.0-0.06*italic)*w, ypart p1 + 0.261 * (ypart p5 - ypart p1));
  p3 = (w/2, ypart p1 + 0.5 * (ypart p5 - ypart p1));
  p4 = rt ((0.0136+0.0833*italic) * w, ypart p5 - 0.261 * (ypart p5 - ypart p1));
  p6 = lft ((0.94+0.06*italic)*w, ypart p5 - (0.140+0.05*italic) * (ypart p5 - ypart p1));
  s = p0 .. controls
    (xpart p0 + (0.13630967632850244 * (xpart p1 - xpart p0)),
      ypart p0 + (0.6173523106060608 * (ypart p1 - ypart p0)))
    and
    (xpart p0 + (0.5636998115942029 * (xpart p1 - xpart p0)),
      ypart p0 + (0.9484581136363636 * (ypart p1 - ypart p0)))
    .. p1 .. controls
    (xpart p1 + (0.46980877419354844 * (xpart p2 - xpart p1)),
      ypart p1 + (0.006359894409937952 * (ypart p2 - ypart p1)))
    and
    (xpart p1 + (0.9765995714285713 * (xpart p2 - xpart p1)),
      ypart p1 + (0.2860573416149067 * (ypart p2 - ypart p1)))
    .. p2 .. controls
    (xpart p2 - (0.05262555760368663 * (xpart p3 - xpart p2)),
      ypart p2 + (0.6083389146341461 * (ypart p3 - ypart p2)))
    and
    (xpart p2 + (0.6820850737327186 * (xpart p3 - xpart p2)),
      ypart p2 + (0.9178271951219512 * (ypart p3 - ypart p2)))
    .. p3 .. controls
    (xpart p3 + (0.38497423979591844 * (xpart p4 - xpart p3)),
      ypart p3 + (0.11381301369862977 * (ypart p4 - ypart p3)))
    and
    (xpart p3 + (1.0201843469387757 * (xpart p4 - xpart p3)),
      ypart p3 + (0.20611376027397257 * (ypart p4 - ypart p3)))
    .. p4 .. controls
    (xpart p4 - (0.029447223350253814 * (xpart p5 - xpart p4)),
      ypart p4 + (0.8799830354609937 * (ypart p5 - ypart p4)))
    and
    (xpart p4 + (0.6673211421319798 * (xpart p5 - xpart p4)),
      ypart p4 + (0.9891482411347523 * (ypart p5 - ypart p4)))
    .. p5 .. controls
    (xpart p5 + (0.3219223015873017 * (xpart p6 - xpart p5)),
      ypart p5 - (0.0033624895833345245 * (ypart p6 - ypart p5)))
    and
    (xpart p5 + (0.7415597460317461 * (xpart p6 - xpart p5)),
      ypart p5 - (0.02329800777599504 * (ypart p6 - ypart p5)))
    .. p6;
  draw s;
  path semicircle;
  pen eraser;
  semicircle =   (0.5, 0)
  .. controls (0.5, 0.13261) and (0.44733, 0.2598) ..
  (0.35356, 0.35356)
  .. controls (0.2598, 0.44733) and (0.13261, 0.5) ..
  (0, 0.5);
  semicircle = semicircle & semicircle rotated 90 -- (0.5, 0) & cycle;
  eraser = makepen(semicircle);
  pickup eraser rotated 16.21+(3.3*(weight-1))-(0.85*italic)
    xscaled ((0.19+0.106*(weight-1)) * w)
    yscaled ((0.13+0.05*(weight-1)) * (h+d));
  erase (point 0 of s);
  pickup eraser rotated 196.21+(3.3*(weight-1))-(0.85*italic)
    xscaled ((0.19+0.106*(weight-1)) * w)
    yscaled ((0.13+0.05*(weight-1)) * (h+d));
  erase (point 6 of s);
endchar;

beginchar("T", (0.54+0.048*(weight-1)+0.017*italic+0.012*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS:
  numeric pen_width, pen_height, italic_shift;
  pen_width = ((0.139+0.104*(weight-1))*h);
  pen_height = ((0.12+0.088*(weight-1))*h);;
  italic_shift = 0;
  % CONTROL POINTS:
  pair p1, p2, p3, p4;
  p1 = (0, h-(pen_height/2));
  p2 = (w, h-(pen_height/2));
  p3 = (w/2 - 0.05 * pen_width * italic, h-(pen_height));
  p4 = (w/2 - 0.24*w*italic+0.03*italic*(weight-1)*w, 0);
  % RENDERING:
  pickup pensquare slanted (0.2*italic) xscaled pen_width yscaled pen_height;
  draw rt p1 -- lft p2;
  pickup pensquare xscaled pen_width;
  draw p3 -- p4;
endchar;

beginchar("U", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS:
  numeric pen_width, pen_height;
  pen_width = ((0.139+0.104*(weight-1))*h);
  pen_height = ((0.139+0.05*(weight-1))*h);
  % CONTROL POINTS
  pair p1, p2, p3, p4, p5;
  p1 = (pen_width/2 + 0.18*italic*w, h);
  p2 = (pen_width/2 + 0.03 *italic*w, 0.35*h);
  p3 = (w - pen_width/2, h);
  p4 = (w - pen_width/2 - 0.15*italic*w, 0.35*h);
  p5 = (w/2 - 0.15*italic*w, pen_height/2);
  % RENDERING:
  pickup pensquare xscaled pen_width;
  draw p1--p2;
  draw p3--p4;
  pickup pencircle xscaled pen_width yscaled pen_height;
  draw p2{p2-p1} .. {1, 0}p5 .. {p3-p4}p4;
endchar;

beginchar("V", (0.57+0.068*(weight-1)+0.003*italic+0.002*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS:
  numeric pen_width, spread, basis_position;
  pen_width = ((0.15+0.09*(weight-1))*h);
  spread = (0.007+0.012*(weight-1))*w;
  basis_position = (0.5-0.25*italic+0.02*italic*(weight-1))*w;
  % CONTROL POINTS
  pair p1, p2a, p2b, p3;
  p1 = (pen_width/2, h);
  p2a = (basis_position - spread, 0);
  p2b = (basis_position + spread, 0);
  p3 = (w - pen_width/2, h);
  % RENDERING:
  pickup pensquare xscaled pen_width;
  draw p1--p2a;
  draw p2b--p3;
endchar;

beginchar("W", (0.822+0.042*(weight-1)+0.003*italic+0.003*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS:
  numeric pen_width, spread, basis_position1, basis_position2, top_position;
  pen_width = ((0.15+0.09*(weight-1))*h);
  spread = (0.007+0.012*(weight-1))*w;
  basis_position1 = (0.267-0.16*italic+0.03*italic*(weight-1))*w;
  basis_position2 = (0.72-0.16*italic-0.017*italic*(weight-1))*w;
  top_position = (0.5-0.01*italic)*w;
  % CONTROL POINTS
  pair p1, p2a, p2b, p3a, p3b, p4a, p4b, p5;
  p1 = (pen_width/2, h);
  p5 = (w - pen_width/2, h);
  p2a = (basis_position1 - spread, 0);
  p2b = (basis_position1 + spread, 0);
  p3a = (top_position - spread, h);
  p3b = (top_position + spread, h);
  p4a = (basis_position2 - spread, 0);
  p4b = (basis_position2 + spread, 0);
  % RENDERING:
  pickup pensquare xscaled pen_width;
  draw p1--p2a;
  draw p2b--p3a;
  draw p3b--p4a;
  draw p4b--p5;
  % RETOUCH
  pair r1, r2, r3;
  pen eraser;
  r1 = (pen_width, h);
  r2 = (basis_position1+0.02*italic*w+0.01*(weight-1)*italic*w, (0.17+0.14*(weight-1)+0.023*italic)*h);
  r3 = (top_position - pen_width/2 - spread + 0.02*(weight-1)*w, h);
  eraser=makepen(r1--r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
  r1 = (top_position + pen_width/2 + spread - 0.02*(weight-1)*w, h);
  r2 = (basis_position2+0.015*italic*w+0.03*(weight-1)*italic*w, (0.17+0.14*(weight-1)+0.01*italic)*h);
  r3 = (w-pen_width, h);
  eraser=makepen(r1--r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
  r1 = (basis_position1+pen_width/2 + spread - 0.03*italic*(weight-1)*w, 0);
  r2 = (top_position-0.032*italic*w-0.017*italic*(weight-1)*w, (0.825-0.125*(weight-1))*h);
  r3 = (basis_position2-pen_width/2 - spread + 0.02*italic*(weight-1)*w, 0);
  eraser=makepen(r1--r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
endchar;

beginchar("X", (0.605+0.076*(weight-1)+0.139*italic+0.01*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS:
  numeric pen_width, pen_width2, top_shift, bottom_shift;
  pen_width = ((0.17+0.12*(weight-1)+0.04*italic*(weight-1))*h);
  pen_width2 = ((0.17+0.12*(weight-1)-0.03*italic*(weight-1))*h);
  top_shift = (0.02+0.2*italic)*w;
  bottom_shift = (0.193*italic-0.01*italic*(weight-1))*w;
  % CONTROL POINTS
  pair p1, p2, p3, p4;
  p1 = (pen_width/2, 0);
  p2 = (w - pen_width/2 - (top_shift*(1-italic)), h);
  p3 = (pen_width2/2 + top_shift, h);
  p4 = (w - pen_width2/2 - bottom_shift, 0);
  % RENDERING
  pickup pensquare xscaled pen_width;
  draw p1--p2;
  pickup pensquare xscaled pen_width2;
  draw p3--p4;
endchar;

beginchar("Y", (0.576+0.093*(weight-1)+0.003*italic+0.009*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS
  numeric pen_width;
  pen_width = ((0.15+0.12*(weight-1)-0.01*italic*(weight-1))*h);
  % CONTROL POINTS
  pair p1, p2, p3, p4;
  p1 = (pen_width/2, h);
  p2 = ((0.5-0.2*italic+0.07*italic*(weight-1))*w, 0.39*h);
  p3 = (w - pen_width/2, h);
  p4 = ((0.5-0.27*italic+0.05*italic*(weight-1))*w, 0);
  % RENDERING
  pickup pensquare xscaled pen_width;
  draw p1--p2;
  draw p3--p2;
  draw p2--p4;
endchar;

beginchar("Z", (0.52+0.038*(weight-1)+0.140*italic+0.01*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS
  numeric pen_width, pen_height, top_margin, bot_margin, ang;
  pen_width = ((0.16+0.125*(weight-1)-0.01*italic*(weight-1))*h);
  pen_height = ((0.123+0.086*(weight-1)-0.01*italic*(weight-1))*h);
  top_margin = (0.025+0.215*italic)*w;
  bot_margin = (0.19*italic)*w;
  ang=-15*italic;
  % CONTROL POINTS
  pair p1, p2, p3, p4, p5, p6;
  p1 = (top_margin, h - pen_height/2 * cosd ang);
  p2 = (w - (top_margin*(1-italic)) + 0.5*pen_height*sind ang, h - pen_height/2 * cosd ang);
  p3 = (-0.5*pen_height*(sind ang), pen_height/2 * cosd ang);
  p4 = (w-bot_margin, pen_height/2 * cosd ang);
  p5 = (w - (top_margin*(1-italic)) - pen_width/2 + pen_height*sind ang, h - pen_height * cosd ang);
  p6 = (pen_width/2 - pen_height*sind ang, pen_height * cosd ang);
  % RENDERING
  pickup pensquare yscaled pen_height rotated ang;
  draw p1--p2;
  draw p3--p4;
  pickup pensquare xscaled pen_width;
  draw p5--p6;
  % RETOUCH
  pair r1, r2, r3;
  pen eraser;
  r1 = p5 + (0.5*pen_width, 0);
  r1 = (0.25*(weight-1))[r1, p2] + (0.01*w*(weight-1)*(1-italic), 0);
  r2 = p6 + (0.5*pen_width, 0);
  r3 = (w, ypart p6);
  eraser = makepen(r1--r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
endchar;

beginchar("[", (0.207+0.05*(weight-1)+0.177*italic+0.003*italic*(weight-1))*size*pt,
    0.668*size*pt, 0.16*size*pt);
  % PARAMETERS
  numeric pen_width, pen_height, top_margin, bot_margin, ang;
  pen_width = ((0.12+0.075*(weight-1)-0.01*italic*(weight-1))*h);
  pen_height = ((0.111+0.066*(weight-1)-0.01*italic*(weight-1))*h);
  bot_margin = (0.43*italic)*w;
  ang=-13*italic-2*italic*(weight-1);
  % CONTROL POINTS
  pair p1, p2, p3, p4, p5, p6;
  p2 = (w, h - pen_height/2 * cosd ang);
  p3 = (0, -d+pen_height/2 * cosd ang);
  p4 = (w-bot_margin, -d+pen_height/2 * cosd ang);
  p6 = (pen_width/2 -pen_height*sind ang, -d+pen_height * cosd ang);
  p5 = (xpart p6 - (h+d-2*pen_height*cosd(ang))*sind(ang) , h - pen_height * cosd ang);
  p1 = (xpart p5 - pen_width/2 - 0.5*pen_height*sind(ang), h-pen_height/2 * cosd ang);
  % RENDERING
  pickup pensquare yscaled pen_height rotated ang;
  draw p1-- lft p2;
  draw rt p3--p4;
  pickup pensquare xscaled pen_width;
  draw p5--p6;
endchar;

beginchar("\\", (0.348+0.065*(weight-1))*size*pt, 0.668*size*pt, 0);
  % PARAMETERS
  numeric pen_width;
  pen_width = (0.25+0.12*(weight-1))*w;
  % CONTROL POINTS
  pair p1, p2;
  p1 = (pen_width/2, h);
  p2 = (w-pen_width/2, 0);
  % RENDERING
  pickup pensquare xscaled pen_width;
  draw p1--p2;
endchar;

beginchar("]", (0.207+0.05*(weight-1)+0.177*italic+0.003*italic*(weight-1))*size*pt,
    0.668*size*pt, 0.16*size*pt);
  % PARAMETERS
  numeric pen_width, pen_height, top_margin, bot_margin, ang;
  pen_width = ((0.12+0.075*(weight-1)-0.01*italic*(weight-1))*h);
  pen_height = ((0.111+0.066*(weight-1)-0.01*italic*(weight-1))*h);
  top_margin = (0.45*italic-0.08*italic*(weight-1))*w;
  ang=-12*italic;
  bot_margin = -(h+d-1.5*pen_height*cosd(ang))*sind(ang)-pen_height*sind(ang);
  % % CONTROL POINTS
  pair p1, p2, p3, p4, p5, p6;
  p1 = (top_margin, h - pen_height/2 * cosd ang);
  p2 = (w, h - pen_height/2 * cosd ang);
  p3 = (0, -d+pen_height/2 * cosd ang);
  p4 = (w-bot_margin, -d+pen_height/2 * cosd ang);
  p5 = (xpart p2 - pen_width/2 +pen_height*sind ang, h - pen_height * cosd ang);
  p6 = (xpart p5 + (h+d-2*pen_height*cosd(ang))*sind(ang) , -d+pen_height * cosd ang);
  % % RENDERING
  pickup pensquare yscaled pen_height rotated ang;
  draw p1-- lft p2;
  draw rt p3--p4;
  pickup pensquare xscaled pen_width;
  draw p5--p6;
endchar;

beginchar("^", (0.447+0.038*(weight-1))*size*pt,
    0.668*size*pt, (-0.309-0.001*(weight-1))*size*pt);
  % PARAMETERS
  numeric pen_width;
  pen_width = (0.14+0.07*(weight-1))*h;
  % CONTROL_POINTS
  pair p1, p2, p3;
  p1 = (pen_width/2, -d);
  p2 = (w/2, h);
  p3 = (w-pen_width/2, -d);
  % RENDERING
  pickup pensquare xscaled pen_width;
  draw p1--p2--p3;
  % RETOUCH
  pair r1, r2, r3;
  pen eraser;
  r1 = (0, -d);
  r2 = (xpart p2 - pen_width/2 + (0.01+0.015*(weight-1))*w, h);
  r3 = (0, h);
  eraser = makepen(r1--r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
  r1 = (w, -d);
  r2 = (xpart p2 + pen_width/2 - (0.01+0.015*(weight-1))*w, h);
  r3 = (w, h);
  eraser = makepen(r1--r2--r3--cycle);
  pickup eraser;
  erase (0, 0);
endchar;

beginchar("_", (0.378+0.033*(weight-1)+0.001*italic*(weight-1))*size*pt,
    0, (0.08+0.049*(weight-1))*size*pt);
  pickup pensquare xscaled w yscaled d;
  draw (w/2, -d/2);
  % NO KERNING
endchar;

beginchar("`", (0.164+0.045*(weight-1)-0.014*italic)*size*pt,
    (0.708+0.021*(weight-1))*size*pt, -(0.572+0.003*(weight-1))*size*pt);
  pair p1, p2, p2b, p3, p4, p5;
  pen p;
  p1 = (0.25*w, h);
  p2 = (0, (0.95-0.025*(weight-1))*h);
  p2b = (1, 0) rotated 225 xscaled (xpart p1 - xpart p2) yscaled (ypart p1 - ypart p2) shifted (xpart p1, ypart p2);
  p3 = ((0.6-0.15*(weight-1))*w, -d);
  p4 = (w, -d);
  p5 = (1, 0) rotated 45 xscaled (xpart p1 - xpart p2) yscaled (ypart p1 - ypart p2) shifted (xpart p1, ypart p2);
  p = makepen(p1{-1, 0}..{0, -1}p2..p2b--p3--p4--p5..cycle);
  pickup p;
  draw (0, 0);
  % NO KERNING
endchar;

beginchar("a", (0.438+0.06*(weight-1)+0.042*italic+0.002*italic*(weight-1))*size*pt,
    (0.504+0.006*(weight-1))*size*pt, 0);
  % PARAMETERS
  numeric pen_width1, pen_height1, pen_width2, pen_height2;
  pen_width1 = (0.155+0.125*(weight-1))*h;
  pen_height1 = (0.13+0.07*(weight-1))*h;
  pen_width2 = (0.18+0.125*(weight-1))*h;
  pen_height2 = (0.13+0.05*(weight-1))*h;
  % CONTROL POINTS
  pair p1, p2, p3, p4, p5, p6;
  pair p7, p8, p9;
  p1 = ((0.16+0.03*(weight-1)+0.07*italic)*w, (0.74-0.02*(weight-1)-0.01*italic)*h);
  p2 = ((0.51+0.01*(weight-1)+0.055*italic)*w, h-pen_height1/2);
  p3 = ((0.85-0.04*(weight-1)+0.02*italic)*w, ypart p1);
  p4 = ((0.87-0.04*(weight-1)-0.04*italic)*w, 0.54*h);
  p6 = ((0.87-0.04*(weight-1)-0.12*italic)*w, 0.25*h);
  p7 = (w- pen_width1/2 - 0.17*italic*w, 0);
  p8 = (pen_width2/2, (0.26-0.03*italic)*h);
  p5 = 0.5[p4, p8] shifted (0, 0.1*h);
  p9 = ((0.38-0.08*italic)*w, pen_height2/2);
  % RENDERING
  pickup pencircle xscaled pen_width1 yscaled pen_height1;
  draw p1..p2{1, 0}..p3..p4..p6..p7;
  pickup pencircle xscaled pen_width2 yscaled pen_height2;
  draw (p4 - ((pen_width2-pen_width1)/2, 0))..p5..p8{0, -1}..p9{1, 0}..{(0, 1) rotated -50}(p6 - ((pen_width2-pen_width1)/2, 0));
  % RETOUCH
  pickup pensemicircle xscaled (pen_width1+0.03*w) yscaled (pen_height1+0.03*h) rotated 160;
  erase p1;
  pair r1, r2, r3, r4, r5;
  pen eraser;
  r1 = p3 - ((0.075+0.05*(weight-1)+0.09*italic)*w, (0.27+0.02*(weight-1))*h);
  r2 = r1 - ((0.035*italic-0.02*italic*(weight-1))*w, (0.25-0.1*(weight-1)-0.01*italic)*h);
  r3 = (xpart p9, pen_height2);
  r5 = (pen_width2, ypart p8);
  r4 = 0.5[r3, r5] - ((0.035-0.02*(weight-1)-0.01*italic)*w, (0.035-0.02*(weight-1)-0.01*italic)*h);
  eraser = makepen(r1--r2..r3..r4..{0, 1}r5--cycle);
  pickup eraser;
  erase (0, 0);
  % KERNING
  % 'v', 'y', '', '': -23 if italic bold
endchar;

beginchar("b", (0.459+0.065*(weight-1)+0.044*italic+0.028*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS
  numeric pen_width1, pen_height2, pen_width2, pen_width3;
  pen_width1 = (0.13+0.1*(weight-1))*h;
  pen_height2 = (0.1+0.065*(weight-1))*h;
  pen_width2 = (0.06+0.01*(weight-1))*h;
  pen_width3 = (0.14+0.1*(weight-1))*h;
  % CONTROL POINTS
  pair p1, p2, p3;
  p1 = (pen_width1/2 + 0.28*italic*w -0.05*italic*(weight-1)*w, h);
  p2 = (pen_width1/2 + 0.025*italic*w, 0.11*h);
  p3 = ((0.2+0.1*italic+0.06*italic*(weight-1)) * pen_width1, 0);
  pair p4, p5, p6, p7;
  p4 = ((0.47-0.1*italic+0.09*italic*(weight-1))*w, pen_height2/2);
  p5 = 0.3[p2, p1] + (0.5*(pen_width1-pen_width2), 0);
  p6 = ((0.47+0.15*italic+0.02*italic*(weight-1))*w, (0.695-0.015*(weight-1)+0.01*italic-0.02*italic*(weight-1))*h);
  p7 = (w-pen_width3/2-0.035*italic*(weight-1)*w, (0.37+0.08*italic)*h);
  % RENDERING
  pickup pensquare xscaled pen_width1;
  draw p1--p2--p3;
  pickup pencircle xscaled pen_width2 yscaled pen_height2;
  draw p4{-1, 0}..p5..{1, 0}p6;
  pickup pencircle xscaled pen_width3 yscaled pen_height2;
  draw p6{1, 0}..p7{0, -1*(1-italic*(weight-1))}..{-1, 0}p4;
  % RETOUCH
  if italic > 0:
    pair r1, r2, r3, r4, r5;
    pen eraser;
    r1 = p1 - (pen_width1/2, 0);
    r2 = 1.5[p1, p2] - (pen_width1/2, 0);
    r3 = (0, h);
    eraser = makepen(r1--r2--r3--cycle);
    pickup eraser;
    erase (0, 0);
  fi
  % NO KERNING
endchar;

beginchar("c", (0.431+0.048*(weight-1)+0.046*italic+0.009*italic*(weight-1))*size*pt,
    (0.504+0.006*(weight-1))*size*pt, 0);
  % PARAMETERS
  numeric pen_width, pen_height;
  pen_width = (0.18+0.12*(weight-1))*h;
  pen_height = (0.145+0.08*(weight-1))*h;
  % CONTROL POINTS
  pair p1, p2, p3, p4, p5;
  pickup pencircle xscaled pen_width yscaled pen_height;
  p1 = lft ((0.993+0.007*italic)*w, 0.762*h);
  p2 = bot ((0.548+0.063*italic)*w, h);
  p3 = rt (0.03*w*italic, (0.501-0.102*italic)*h);
  p4 = top ((0.533-0.098*italic)*w, 0);
  p5 = lft ((1-0.1*italic)*w, 0.238*h);
  % RENDERING
  draw p1..{-1, 0}p2..{0, -1}p3..{1, 0}p4..p5;
  % RETOUCH
  pickup pensemicircle xscaled (pen_width*1.1) yscaled (pen_height*1.1) rotated (200) ;
  erase p1;
  pickup pensemicircle xscaled (pen_width*1.1) yscaled (pen_height*1.1) rotated (-20);
  erase p5;
  % KERNING:
  % 'w': -28
endchar;

beginchar("d", (0.476+0.064*(weight-1)+0.081*italic+0.012*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
  % PARAMETERS
  numeric pen_width1, pen_height2, pen_width2, pen_width3;
  pen_width1 = (0.13+0.1*(weight-1))*h;
  pen_height2 = (0.1+0.065*(weight-1))*h;
  pen_width2 = (0.06+0.00*(weight-1))*h;
  pen_width3 = (0.14+0.1*(weight-1))*h;
  % CONTROL POINTS
  pair p1, p2, p3;
  p1 = (0.96*w-pen_width1/2 + 0.04*italic*w, h);
  p2 = (0.96*w-pen_width1/2 - 0.19*italic*w + 0.04*italic*(weight-1)*w, 0.11*h);
  p3 = (w-pen_width1/2 - (0.23-0.03*(weight-1))*italic*w, 0);
  pair left_side, p4, p5, p6, p7;
  p4 = ((0.46-0.055*(weight-1)-0.12*italic+0.02*italic*(weight-1))*w, pen_height2/2);
  p5 = (0.31+0.06*italic)[p2, p1] - (0.5*(pen_width1-pen_width2), 0);
  p6 = ((0.46-0.055*(weight-1)+0.02*italic+0.04*italic*(weight-1))*w, (0.7-0.015*(weight-1)+0.0*italic-0.02*italic*(weight-1))*h);
  %p7 = (pen_width3/2-0.035*italic*(weight-1)*w, ypart p5);
  left_side = (0, ypart p5) + ((0.03+0.01*(weight-1))*italic*w, -0.135*italic*h);
  p7 = left_side + (pen_width3/2, 0);
  % RENDERING
  pickup pensquare xscaled pen_width1;
  draw p1--p2--p3;
  pickup pencircle xscaled pen_width2 yscaled pen_height2;
  draw p4{1, 0}..p5..{-1, 0}p6;
  pickup pencircle xscaled pen_width3 yscaled pen_height2;
  draw p6{-1, 0}..p7{0, -1}..{1, 0}p4;
  % RETOUCH
  pair r1, r2, r3, r4;
  r1 = p5 - (pen_width2/2 + (0.01-0.01*(weight-1))*italic*w, 0);
  r2 = p6 - ((-0.1*(weight-1)+0.08*italic*(weight-1))*w, pen_height2/2 + 0.015*(weight-1)*h - 0.02*italic*(weight-1)*h);
  r3 = p7 + (pen_width3/2 - 0.01*w*italic, (-0.01*(weight-1)-0.008*italic+0.045*italic*(weight-1))*h);
  r4 = p4 + ((0.09*(weight-1)+0.04*italic-0.02*italic*(weight-1))*w, pen_height2/2 - 0.01*italic*h);
  pickup pencircle scaled 0.08*h;
  draw r1..r2..r3..r4..cycle;
  pen eraser;
  eraser = makepen(r1..r2..r3..r4..cycle);
  pickup eraser;
  erase (0, 0);
  % KERNING: 'j': 17 if bold
endchar;

beginchar("e", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("f", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("g", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("h", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("i", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("j", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("k", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("l", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("m", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("n", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("o", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("p", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("q", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("r", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("s", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("t", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("u", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("v", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("w", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("x", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("y", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("z", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("{", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("|", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("}", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;

beginchar("~", (0.54+0.036*(weight-1)+0.127*italic-0.004*italic*(weight-1))*size*pt,
    0.668*size*pt, 0);
endchar;





